import java.security.InvalidKeyException

apply plugin: 'com.android.application'
android {
    def globalConfiguration = rootProject.extensions.getByName("ext")
    flavorDimensions "dev"
    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        applicationId globalConfiguration["androidApplicationId"]
        versionCode globalConfiguration["androidVersionCode"]
        versionName globalConfiguration["androidVersionName"]
        testInstrumentationRunner globalConfiguration["testInstrumentationRunner"]
        testApplicationId globalConfiguration["testApplicationId"]
//        flavorSelection "dev", 'development'
    }

    signingConfigs {
        try {
            release {
                keyAlias 'thirdapp.keystore'
                keyPassword KEY_PASSWORD
                storeFile file('E:/key/rockhippo7000.keystore')
                storePassword KEYSTORE_PASSWORD
            }
        }
        catch (ignored) {
            throw new InvalidKeyException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
        }
        debug {
            try {
                keyAlias 'thirdapp.keystore'
                keyPassword KEY_PASSWORD
                storeFile file('E:/key/rockhippo7000.keystore')
                storePassword KEYSTORE_PASSWORD
            }
            catch (ignored) {
                throw new InvalidKeyException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    buildTypeMatching  'debug', 'release'

    productFlavors {
        development {
            minSdkVersion 21
            dimension "dev"
            resConfigs "xxhdpi"
        }
        production {
            dimension "dev"
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            // 开启ZipAlign优化
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        //开发版本
        debug {
            signingConfig signingConfigs.debug
            shrinkResources = false
            minifyEnabled false
            ext.alwaysUpdateBuildId = false
            zipAlignEnabled true
        }
    }

    buildToolsVersion '26.0.2'
}
android.applicationVariants.all { variant ->
    variant.outputs.all {
        if (variant.buildType.name.equals('debug')) {
            outputFileName = "framMVP-${variant.name}-t${variant.versionName}-${variant.versionCode}.apk"
        }
        if (variant.buildType.name.equals('release')) {
            outputFileName = "framMVP-${variant.name}-v${variant.versionName}-${variant.versionCode}-${getApkCreateDate()}.apk"
        }
    }
}
//获取时间戳
def getApkCreateDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy.MM.dd.HH.mm')
    return formattedDate
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    debugCompile 'com.amitshekhar.android:debug-db:1.0.0'
    def appDependencies = rootProject.ext.appDependencies
    compile appDependencies.butterKnife
    compile appDependencies.rxpermissions
    compile appDependencies.recyclerView
    compile appDependencies.v7compat
    compile appDependencies.design
    compile appDependencies.drawable_vector
    compile appDependencies.barlibrary
    compile appDependencies.v4compat
    compile project(':ilibray')
    annotationProcessor appDependencies.butterCompiler
    testCompile 'junit:junit:4.12'
}
